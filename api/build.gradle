// ongi/api/build.gradle

plugins {
	id 'org.springframework.boot'
	id 'io.spring.dependency-management'
	id 'org.asciidoctor.jvm.convert'
}


ext {
	set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
	implementation project(':recipe-domain')
	implementation project(':ingredients-domain')
	implementation project(':user-domain')

	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
	implementation 'org.springframework.boot:spring-boot-starter-security'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation libs.mysql.connector
	// QueryDSL
	implementation libs.querydsl.core
	implementation libs.querydsl.sql
	implementation libs.querydsl.sql.spring

	implementation "com.querydsl:querydsl-jpa:${libs.versions.querydsl.get()}:jakarta"
	annotationProcessor "com.querydsl:querydsl-apt:${libs.versions.querydsl.get()}:jakarta"
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

	// redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.apache.commons:commons-pool2'

	// kafka
	implementation 'org.springframework.kafka:spring-kafka'
	implementation 'org.springframework.boot:spring-boot-starter-kafka'

	// log
	implementation libs.log4jdbc
	implementation libs.log4j.api

	// JWT
	implementation libs.jjwt.api
	runtimeOnly libs.jjwt.impl
	runtimeOnly libs.jjwt.jackson

	testImplementation 'org.springframework.boot:spring-boot-starter-actuator-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-validation-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webflux-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testRuntimeOnly libs.h2.database
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named("bootJar") {
	archiveClassifier = 'boot'
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.named('asciidoctor') {
	inputs.dir snippetsDir
	dependsOn test
}
